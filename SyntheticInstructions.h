;Synthetic Instructions
;Additional instructions that would be nice to have
;that can be made with the native instruction set
;===============================================================================
;Negative A (neg)
;===============================================================================
; A = -A
;-------------------------------------------------------------------------------
	MAC neg
	eor #$ff
	sec
	adc #0
	ENDM
;===============================================================================
;Reverse subtract (rsb)
;===============================================================================
; A = Value - A
;-------------------------------------------------------------------------------
	MAC rsb
.Value	SET {1}
	eor #$ff
	sec
	adc #.Value
	ENDM
;===============================================================================
;16-bit inc (incw)
;===============================================================================
;16-bit increment Word
;-------------------------------------------------------------------------------
	MAC incw
.Word	SET {1}
	inc .Word
	bne .noinc
	inc .Word+1
.noinc:
	ENDM
;===============================================================================
;16-bit dec (decw)
;===============================================================================
;16-bit decrement Word
;-------------------------------------------------------------------------------
	MAC decw
.Word	SET {1}
	pha
	lda .Word
	bne .nodec
	dec .Word+1
.nodec:
	pla
	dec .Word
	ENDM
;===============================================================================
;Transfer X to Y (txy)
;===============================================================================
;
;-------------------------------------------------------------------------------
	MAC txy
	pha
	txa
	tay
	pla
	ENDM
;===============================================================================
;Transfer Y to X (tyx)
;===============================================================================
;
;-------------------------------------------------------------------------------
	MAC tyx
	pha
	tya
	tax
	pla
	ENDM
;===============================================================================
;Jump (indirect,X)
;===============================================================================
;
;-------------------------------------------------------------------------------
	MAC jmp(ind,x)
.Addr	SET {1}
	lda .Addr+1,x
	pha
	lda .Addr,x
	pha
	php
	rti
	ENDM
;===============================================================================
;Swap 
;===============================================================================
; A = $HL -> $LH
;-------------------------------------------------------------------------------
	MAC SWP
	asl
	adc #$80
	rol
	asl
	adc #$80
	rol
	ENDM